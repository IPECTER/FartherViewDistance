plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version "1.5.3" apply false
}

version = "10.2.0"
group = "xuan.cat.fartherviewdistance"

configurations {
    extraLibs
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenLocal()
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.2'

    extraLibs project(path: ':NMS')
    extraLibs project(path: ':NMS_1_18_R2', configuration: 'reobf')
    extraLibs project(path: ':NMS_1_19_R1', configuration: 'reobf')
    extraLibs project(path: ':NMS_1_19_R2', configuration: 'reobf')
    extraLibs project(path: ':NMS_1_19_R3', configuration: 'reobf')
    extraLibs project(path: ':NMS_1_20_R1', configuration: 'reobf')
    extraLibs project(path: ':NMS_1_20_R2', configuration: 'reobf')
    extraLibs project(path: ':NMS_1_20_R3', configuration: 'reobf')

    implementation project(path: ':NMS')
    implementation project(path: ':NMS_1_18_R2', configuration: 'reobf')
    implementation project(path: ':NMS_1_19_R1', configuration: 'reobf')
    implementation project(path: ':NMS_1_19_R2', configuration: 'reobf')
    implementation project(path: ':NMS_1_19_R3', configuration: 'reobf')
    implementation project(path: ':NMS_1_20_R1', configuration: 'reobf')
    implementation project(path: ':NMS_1_20_R2', configuration: 'reobf')
    implementation project(path: ':NMS_1_20_R3', configuration: 'reobf')
}


def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
